/* 功能作用: 在行走时进行快踢, 节省时间. 包括前踢, 左,右侧踢.
 * 建议在Nao距离球300范围内开始调整位置, 即该option的进入条件: ball.distance < 300
 * 该option适用于实体机器人Nao.
 */
option walk_kick
{
	/* The quick kick ids.*/
	enum motion.kick_in_walk @id;
	
	initial state set_none
	{
		decision
		{
			if(state_time > 100)
				goto decide_kick;
			else
				stay;
		}
		action
		{
			/* 每次执行quick kick之前必须先将kick_in_walk类型设置为none.*/
			motion.kick_in_walk = none;
			/* 踢球时头应该看着, 保证踢球时的精确度.*/
			head.control_mode = state_time % 2000 < 777 ? look_down : look_at_ball;
		}
	}
	
	state decide_kick
	{
		decision
		{	/* 使用左脚向前快踢.*/
			if(@id == left)
				goto check_pose_for_kick_forward_using_left_foot;
			/* 使用右脚向前快踢.*/
			else if(@id == right)
				goto check_pose_for_kick_forward_using_right_foot;
			/* 使用左脚向右侧踢.*/
			else if(@id == sidewards_left)
				goto check_pose_for_sidewards_left;
			/* 使用右脚向前侧踢.*/
			else if(@id == sidewards_right)
				goto check_pose_for_sidewards_right;
			else
				stay;
		}
		action
		{
		}
	}
	
	state check_pose_for_kick_forward_using_left_foot
	{
		decision
		{
			if(between(value = ball.x, min = 150, max = 180)
			&& between(value = ball.y, min = 30, max = 100))
				goto kick;
			else
				stay;
		}
		action
		{
			motion.walk_target.x = ball.x - 165;
      		motion.walk_target.y = ball.y - 60;
      		motion.walk_target.rot = 0;
      		motion.walk_speed = 70;
		}
	}
	
	state check_pose_for_kick_forward_using_right_foot
	{
		decision
		{
			if(between(value = ball.x, min = 150, max = 180)
			&& between(value = ball.y, min = -100, max = -30))
				goto kick;
			else
				stay;
		}
		action
		{
			motion.walk_target.x = ball.x - 165;
      		motion.walk_target.y = ball.y + 60;
      		motion.walk_target.rot = 0;
      		motion.walk_speed = 70;
		}
	}
	
	state check_pose_for_sidewards_left
	{
		decision
		{
			if(between(value = ball.x, min = 140, max = 170) && abs(value = ball.y) < 10)
				goto kick;
			else
				stay;
		}
		action
		{
			motion.walk_target.x = ball.x - 155;
      		motion.walk_target.y = ball.y;
      		motion.walk_target.rot = 0;
      		motion.walk_speed = 70;
		}
	}
	
	state check_pose_for_sidewards_right
	{
		decision
		{
			if(between(value = ball.x, min = 140, max = 170) && abs(value = ball.y) < 10)
				goto kick;
			else
				stay;
		}
		action
		{
			motion.walk_target.x = ball.x - 155;
      		motion.walk_target.y = ball.y;
      		motion.walk_target.rot = 0;
      		motion.walk_speed = 70;
		}
	}
	state kick
	{
		decision
		{
			/* 若球没有踢出范围内, 需再次踢.*/
			if(state_time > 1000)
				goto set_none;
			else
				stay;
		}
		action
		{
			motion.kick_in_walk = @id;
		}
	}
}