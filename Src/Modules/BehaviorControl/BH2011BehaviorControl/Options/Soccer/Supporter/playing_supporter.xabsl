/** A test supporter in BH2011 with common decision */
option playing_supporter
{
	common decision
	{
		if(ball.time_since_last_seen > 8000)
			goto search_for_ball;
		else if(ball.time_since_last_seen > 2000)
			goto normal_playing;
		else if(ball.seen.distance < 1000 && ball.speed.robot.x < -220 && ball.speed.robot.y < 0)
			goto block_left;
		else if(ball.seen.distance < 1000 && ball.speed.robot.x < -220 && ball.speed.robot.y > 0)
			goto block_right;
		else if(ball.position.field.x < -2400)
			goto intercept;
		else if(ball.seen.distance < 300)
			goto support_kick;
	}

	initial state already
	{
		decision
		{
			else if(state_time > 1000)
				goto normal_playing;
			else
				stay;
		}
		action
		{
			motion.type = stand;
			head.control_mode = look_up_and_down;
		}
	}

	state search_for_ball
	{
		decision
		{
			else if(ball.was_seen)
				goto normal_playing;
			else
				stay;
		}
		action
		{
			motion.type = walk;
			motion.walk_target.x = 0;
			motion.walk_target.y = 0;
			motion.walk_target.rot = ball.y < 0 ? -180 : 180;
			motion.walk_speed = 50;
			head.control_mode = look_up_and_down;
		}
	}

	state normal_playing
	{
		action
		{
			normal_playing_supporter();
		}
	}

	state intercept
	{
		decision
		{
			else if(ball.time_since_last_seen > 300 || abs(value = ball.speed.robot.x) < 100)
				goto normal_playing;
			else
				stay;
		}
		action
		{
			motion.type = walk;
			motion.walk_target.x = ball.x - 1500;
			motion.walk_target.y = ball.y;
			motion.walk_target.rot = ball.angle;
			motion.walk_speed = 100;
			head.control_mode = look_at_ball;
		}
	}

	state block_left
	{
		decision
		{
			else if(ball.seen.distance > 800 && ball.speed.robot.x > -50)
				goto normal_playing;
			else
				stay;
			
		}
		action
		{
			motion.type = walk;
			motion.walk_target.x = 0;
			motion.walk_target.y = ball.y;
			motion.walk_target.rot = ball.angle;
			motion.walk_speed = 100;
			head.control_mode = look_at_ball;
		}
	}

	state block_right
	{
		decision
		{
			else if(ball.distance > 800 && ball.speed.robot.x > -50)
				goto normal_playing;
			else
				stay;
		}
		action
		{
			motion.type = walk;
			motion.walk_target.x = 0;
			motion.walk_target.y = ball.y;
			motion.walk_target.rot = ball.angle;
			motion.walk_speed = 100;
			head.control_mode = look_at_ball;
		}
	}

	state support_kick
	{
		decision
		{
			else if(ball.seen.distance > 500)
				goto normal_playing;
			else
				stay;
		}
		action
		{
			support_kick();
		}
	}
}