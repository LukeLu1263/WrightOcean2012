option look_at_ball
{
  common decision
  {
    if(ball.time_since_disappeared == 0)
      goto look_at_ball;  
    else if(ball.isSeenTeam)
      goto look_at_ball_team;
  }
  initial state look_at_ball
  {
    decision
    {
      else if(ball.time_since_disappeared > 500)
        goto look_at_ball_team;
      else
        stay;
    }
    action
    {
      head.use_active_vision = false;
      head.mode = target_on_ground;
      head.target.x = ball.x;
      head.target.y = ball.y;
      head.target.z = 35;
      head.speed = 100;
    }
  }

  state look_at_ball_team
  {
    decision
    {
      else if(!ball.isSeenTeam)
        goto look_active_for_ball;
      else
        stay;
    }
    action
    {
      head.mode = target_on_ground;
      head.target.x = locator.field_to_relative.x(x=ball.team.x,y=ball.team.y);
      head.target.y = locator.field_to_relative.y(x=ball.team.x,y=ball.team.y);
      head.target.z = 35;
      head.speed = 100;
    }
  }

  state look_active_for_ball
  {
    decision
    {
      else if(state_time > 2000)
        goto look_active;
      else
        stay;
    }
    action
    {
      head.use_active_vision = true;
      head.active_vision_ball_factor = 1;
      head.speed = 100;
    }
  }
  state look_active
  {
    decision
    {
      else
        stay;
    }
    action
    {
      head.use_active_vision = true;
      head.active_vision_ball_factor = 0;
      head.speed = 100;
    }
  }
}
